apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

sourceCompatibility = "1.6";
targetCompatibility = "1.6";

group = 'hu.webarticum'
version = '0.5'
description = 'Minimalist library for composing simple spreadsheets'

def scmUrl = 'https://github.com/davidsusu/simple-spreadsheet-writer.git'
def websiteUrl = scmUrl.replaceAll(/\.git$/, "");

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
    implementation 'junit:junit:4.12' // XXX
    
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'org.apache.poi:poi:3.14'
    implementation 'org.apache.poi:poi-ooxml:3.14'
    implementation 'org.apache.odftoolkit:simple-odf:0.8.1-incubating'
}

signing {
    required { true }
    sign configurations.archives
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (hasProperty('mavenRepositoryUrl')) uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: project.ext.mavenRepositoryUrl) {
                authentication(
                    userName: project.ext.mavenRepositoryUsername,
                    password: project.ext.mavenRepositoryPassword
                )
            }
            beforeDeployment { MavenDeployment deployment ->
                signing.signPom(deployment)
            }
            pom.artifactId = project.name
            pom.project {
                name = project.name
                description = project.description
                url = websiteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    connection 'scm:git:' + scmUrl
                    developerConnection 'scm:git:' + scmUrl
                    url scmUrl
                }
                developers {
                    developer {
                        id = 'davidsusu'
                        name = 'Dávid Horváth'
                        email = 'horvath@webarticum.hu'
                    }
                }
            }
        }
    }
}
